
Процедура КнопкаВыполнитьНажатие(Кнопка)
	Если Не ЗначениеЗаполнено(СтоимостьРейсаТорги) Тогда
		ПоказатьОповещениеПользователя(,,"Не указана <СтоимостьРейсаТорги>.");
		Возврат;
	КонецЕсли;
	
	Для Каждого Стр Из ТабВодителей Цикл
		Если Стр.Пометка Тогда
			Если 	ИТС_ОбщийМодуль.ОтправитьSMS(Стр.Телефон, ШаблонТекстаSMS) Тогда
				НаборЗаписей = РегистрыСведений.ИТС_SMSСообщенияОРейсах.СоздатьНаборЗаписей();
				Запись = НаборЗаписей.Добавить();
				Запись.Период = ТекущаяДата();
				Запись.Рейс = Рейс;
				Запись.Водитель = Стр.Водитель;
				Запись.ТипSMSСообщения = Перечисления.ИТС_ТипыSMSСообщений.ВакансияНаРейс;
				НаборЗаписей.Записать(Ложь);
			КонецЕсли;
		КонецЕсли;  
	КонецЦикла;
	Об = Рейс.ПолучитьОбъект();
	Об.СтоимостьРейсаТорги = СтоимостьРейсаТорги;
	Об.Записать(РежимЗаписиДокумента.Запись);
КонецПроцедуры

Процедура ЗаполнитьСписокРейсов()
	СписокВыбора = Новый СписокЗначений;
	Для Каждого Стр Из СводнаяИнформацияПоМашинамНаДень Цикл
		Если ЗначениеЗаполнено(Стр.Рейс) И Не ЗначениеЗаполнено(Стр.Машина) Тогда
		  СписокВыбора.Добавить(Стр.Рейс, Стр.Рейс);
		КонецЕсли;  
	КонецЦикла;	
	ЭлементыФормы.Рейсы.СписокВыбора = СписокВыбора;
КонецПроцедуры

Процедура ПриОткрытии()
	Рейс = Неопределено;
	ВесРейса = 0;
	ТочекРейса = 0;
	ЗаполнитьСписокРейсов();
	ЗаполнитьОтборПоМашинам();
КонецПроцедуры

Процедура ЗаполнитьОтборПоМашинам()
	ЭлементОтбора = ПостроительОтчета.Отбор.Добавить("Грузоподъемность");
	Если ЭлементОтбора <> Неопределено Тогда
		ЭлементОтбора.ВидСравнения = ВидСравнения.ИнтервалВключаяГраницы;
		ЭлементОтбора.Использование = Истина;
	КонецЕсли;	
КонецПроцедуры

Процедура КоманднаяПанель1Заполнить(Кнопка)
	ТабВодителей.Очистить();
	
	ПостроительОтчета.Параметры.Вставить("НаДату", ДатаДоставкиПокупателю);
	ПостроительОтчета.Параметры.Вставить("Вид", Справочники.ВидыКонтактнойИнформации.НайтиПоНаименованию("Телефон физ.лица мобильный"));
	ПостроительОтчета.Параметры.Вставить("Тип", Перечисления.ТипыКонтактнойИнформации.Телефон);
	
	ПостроительОтчета.Выполнить();
	
	Выборка = ПостроительОтчета.Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
	  НовСтр = ТабВодителей.Добавить();
	  ЗаполнитьЗначенияСвойств(НовСтр, Выборка);
    КонецЦикла;  
  
    ЭлементыФормы.ТабВодителей.ОбновитьСтроки();
	
КонецПроцедуры

Процедура КоманднаяПанель1Действие1(Кнопка)
	Для Каждого Стр Из ТабВодителей Цикл
		Стр.Пометка = Истина;
	КонецЦикла;	
КонецПроцедуры

Процедура КоманднаяПанель1Действие2(Кнопка)
	Для Каждого Стр Из ТабВодителей Цикл
		Стр.Пометка = Ложь;
	КонецЦикла;	
КонецПроцедуры

Процедура РейсыПриИзменении(Элемент)
	Рейс = Элемент.Значение;
	Если ЗначениеЗаполнено(Рейс) Тогда
		ВесРейса = ПолучитьВесРейса(Рейс);
		ТочекРейса = ПолучитьТочекРейса(Рейс);
		СтоимостьРейсаТорги = Рейс.СтоимостьРейсаТорги;
	Иначе
		ВесРейса = 0;
		ТочекРейса = 0;
		СтоимостьРейсаТорги = 0;
	КонецЕсли;
	СформироватьШаблонТекстаSMS();
КонецПроцедуры

Функция ПолучитьТочекРейса(Рейс)
	//число различных адресов доставки из табличной части док рейс
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ПОДСТРОКА(ИТС_МашинаЗаказа.ЗаказПокупателя.АдресДоставки, 1, 1000) КАК АдресДоставки
	                      |ИЗ
	                      |	РегистрСведений.ИТС_МашинаЗаказа КАК ИТС_МашинаЗаказа
	                      |ГДЕ
	                      |	ИТС_МашинаЗаказа.Регистратор = &Рейс
	                      |ИТОГИ
	                      |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ АдресДоставки)
	                      |ПО
	                      |	ОБЩИЕ");
	Запрос.УстановитьПараметр("Рейс", Рейс);	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Результат = Выборка.АдресДоставки;
	Иначе
		Результат = 0;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции	

Процедура СформироватьШаблонТекстаSMS()
	ШаблонТекстаSMS = "ООО ""ИМИК Лубрикантс"" * Работа * Водитель на доставку.Рейс №" + СокрЛП(Рейс.Номер) + " Вес: " + Формат(ВесРейса,"ЧЦ=15; ЧДЦ=3; ЧН=") + " , Точек: " + Формат(ТочекРейса, "ЧЦ=10; ЧН=") + " , Стоимость : " + Формат(СтоимостьРейсаТорги, "ЧЦ=12; ЧДЦ=2; ЧН=") + " руб.";
КонецПроцедуры                                                                                                                                             

Функция ПолучитьВесРейса(Рейс)
	НайдСтр = СводнаяИнформацияПоМашинамНаДень.Найти(Рейс, "Рейс");
	
	Если НайдСтр = Неопределено Тогда
		Результат = 0;
	Иначе
		Результат = НайдСтр.Вес;
	КонецЕсли;	
	
	Возврат Результат;
КонецФункции

Процедура СтоимостьРейсаТоргиПриИзменении(Элемент)
	СформироватьШаблонТекстаSMS();
КонецПроцедуры

ПостроительОтчета.Текст = "ВЫБРАТЬ
                          |	ВидыТранспорта.Код КАК НомерМашины,
                          |	ВидыТранспорта.Ссылка КАК Машина,
                          |	ВидыТранспорта.Грузоподъемность
                          |ПОМЕСТИТЬ ВТМашины
                          |ИЗ
                          |	Справочник.ВидыТранспорта КАК ВидыТранспорта
                          |{ГДЕ
                          |	ВидыТранспорта.Грузоподъемность}
                          |;
                          |
                          |////////////////////////////////////////////////////////////////////////////////
                          |ВЫБРАТЬ
                          |	ВТМашины.НомерМашины,
                          |	ВТМашины.Машина,
                          |	ИТС_ВодителиМашинСрезПоследних.Водитель
                          |ПОМЕСТИТЬ ВТМашиныВодители
                          |ИЗ
                          |	ВТМашины КАК ВТМашины
                          |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИТС_ВодителиМашин.СрезПоследних(&НаДату, ) КАК ИТС_ВодителиМашинСрезПоследних
                          |		ПО ВТМашины.Машина = ИТС_ВодителиМашинСрезПоследних.Машина
                          |;
                          |
                          |////////////////////////////////////////////////////////////////////////////////
                          |ВЫБРАТЬ
                          |	ВТМашиныВодители.НомерМашины,
                          |	ВТМашиныВодители.Машина,
                          |	ВТМашиныВодители.Водитель КАК Водитель,
                          |	ПОДСТРОКА(КонтактнаяИнформация.Представление, 1, 20) КАК Телефон,
                          |	ИСТИНА КАК Пометка
                          |ИЗ
                          |	ВТМашиныВодители КАК ВТМашиныВодители
                          |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
                          |		ПО ВТМашиныВодители.Водитель = КонтактнаяИнформация.Объект
                          |			И (КонтактнаяИнформация.Тип = &Тип)
                          |			И (КонтактнаяИнформация.Вид = &Вид)
                          |
                          |УПОРЯДОЧИТЬ ПО
                          |	Водитель";